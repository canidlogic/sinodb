#!/usr/bin/env perl
use strict;
use warnings;

# Core dependencies
use Encode qw(decode);
use Unicode::Normalize;

# Sino imports
use Sino::DB;
use SinoConfig;

=head1 NAME

check_merge.pl - Look for ambiguous and missing mappings from the Sino
DB to a CC-CEDICT index file.

=head1 SYNOPSIS

  ./check_merge.pl < dictindex.txt

=head1 DESCRIPTION

This script checks the connection between data in a CC-CEDICT index file
generated by the dictkey.pl script and the TOCFL data currently in the
Sino database, entered with the import_tocfl.pl script.

=cut

# ==================
# Program entrypoint
# ==================

# Check that no parameters
#
($#ARGV < 0) or die "Not expecting program arguments, stopped";

# Switch input and output to UTF-8 and CR+LF decoding
#
binmode(STDIN,  ":encoding(UTF-8) :crlf") or
  die "Failed to set UTF-8 input, stopped";
binmode(STDERR, ":encoding(UTF-8) :crlf") or
  die "Failed to set UTF-8 output, stopped";
binmode(STDOUT, ":encoding(UTF-8) :crlf") or
  die "Failed to set UTF-8 output, stopped";

# Assemble the index in a main hash
#
my %main_hash;
for(my $ltext = <STDIN>; defined($ltext); $ltext = <STDIN>) {
  # Drop line break
  chomp $ltext;
  
  # Skip blank lines
  if ($ltext =~ /\A[ \t]*\z/) {
    next;
  }
  
  # Parse the index line
  ($ltext =~ /\A([^ ]+ [^ ]+) ([^ ]+)\z/) or
    die "Invalid index, stopped";
  my $key = $1;
  my $val = int($2);
  
  # Add to memory index
  $main_hash{$key} = $val;
}

# Create a fallback array, where each element is a subarray storing an
# integer that is 1 for tonal, 0 for toneless, and -1 for no Pinyin, and
# then a hash reference
#
my @fallback;

# First in the fallback array is everything but proper names from the
# main hash, with full tones
#
my $hr = { };
for my $k (keys %main_hash) {
  # Skip * keys
  if ($k =~ /\*/) {
    next;
  }
  
  # Add mapping
  $hr->{$k} = $main_hash{$k};
}
push @fallback, ([1, $hr]);

# Second in the fallback array is proper names from the main hash, with
# full tones
#
$hr = { };
for my $k (keys %main_hash) {
  # Skip all but * keys
  unless ($k =~ /\*/) {
    next;
  }
  
  # Modified key
  my $mk = $k;
  $mk =~ s/\*//g;
  
  # Add mapping
  $hr->{$mk} = $main_hash{$k};
}
push @fallback, ([1, $hr]);

# Third in the fallback array is anything but proper names from the main
# hash, with no tones
#
$hr = { };
for my $k (keys %main_hash) {
  # Skip * keys
  if ($k =~ /\*/) {
    next;
  }
  
  # Modified key
  my $mk = $k;
  $mk = NFD($mk);
  $mk =~ s/[\x{300}\x{301}\x{304}\x{30c}]//g;
  $mk = NFC($mk);
  
  # Get value
  my $val = $main_hash{$k};
  
  # Add mapping to new hash, setting ambiguous if already ambiguous or
  # if there is a current value that is different
  if (defined $hr->{$mk}) {
    # Defined -- if current value is different, change to ambiguous
    if ($hr->{$mk} != $val) {
      $hr->{$mk} = -1;
    }
    
  } else {
    # Not defined yet, so enter new record
    $hr->{$mk} = $val;
  }
}
push @fallback, ([0, $hr]);

# Fourth in the fallback array is proper names from the main hash, with
# no tones
#
$hr = { };
for my $k (keys %main_hash) {
  # Skip everything but * keys
  unless ($k =~ /\*/) {
    next;
  }
  
  # Modified key
  my $mk = $k;
  $mk =~ s/\*//g;
  $mk = NFD($mk);
  $mk =~ s/[\x{300}\x{301}\x{304}\x{30c}]//g;
  $mk = NFC($mk);
  
  # Get value
  my $val = $main_hash{$k};
  
  # Add mapping to new hash, setting ambiguous if already ambiguous or
  # if there is a current value that is different
  if (defined $hr->{$mk}) {
    # Defined -- if current value is different, change to ambiguous
    if ($hr->{$mk} != $val) {
      $hr->{$mk} = -1;
    }
    
  } else {
    # Not defined yet, so enter new record
    $hr->{$mk} = $val;
  }
}
push @fallback, ([0, $hr]);

# Fifth in the fallback array is anything but proper names from the main
# hash, with no Pinyin
#
$hr = { };
for my $k (keys %main_hash) {
  # Skip * keys
  if ($k =~ /\*/) {
    next;
  }
  
  # Modified key
  ($k =~ /\A([^ ]+)/) or die "Unexpected";
  my $mk = $1;
  
  # Get value
  my $val = $main_hash{$k};
  
  # Add mapping to new hash, setting ambiguous if already ambiguous or
  # if there is a current value that is different
  if (defined $hr->{$mk}) {
    # Defined -- if current value is different, change to ambiguous
    if ($hr->{$mk} != $val) {
      $hr->{$mk} = -1;
    }
    
  } else {
    # Not defined yet, so enter new record
    $hr->{$mk} = $val;
  }
}
push @fallback, ([-1, $hr]);

# Sixth in the fallback array is proper names from the main hash, with
# no Pinyin
#
$hr = { };
for my $k (keys %main_hash) {
  # Skip everything but * keys
  unless ($k =~ /\*/) {
    next;
  }
  
  # Modified key
  ($k =~ /\A([^ ]+)/) or die "Unexpected";
  my $mk = $1;
  
  # Get value
  my $val = $main_hash{$k};
  
  # Add mapping to new hash, setting ambiguous if already ambiguous or
  # if there is a current value that is different
  if (defined $hr->{$mk}) {
    # Defined -- if current value is different, change to ambiguous
    if ($hr->{$mk} != $val) {
      $hr->{$mk} = -1;
    }
    
  } else {
    # Not defined yet, so enter new record
    $hr->{$mk} = $val;
  }
}
push @fallback, ([-1, $hr]);

# Open database connection to existing database
#
my $dbc = Sino::DB->connect($config_dbpath, 0);

# Start a read transaction for everything
#
my $dbh = $dbc->beginWork('r');

# Get the IDs of all words
#
my $words = $dbh->selectall_arrayref(
              'SELECT wordid FROM word ORDER BY wordid ASC');
(ref($words) eq 'ARRAY') or die "No records to examine, stopped";

# Go through each word
#
for my $word (@$words) {
  # Get the word ID
  my $wordid = $word->[0];
  
  # Get all traditional character renderings
  my $tcr = $dbh->selectall_arrayref(
              'SELECT hantrad FROM han WHERE wordid=? '
              . 'ORDER BY hanord ASC',
              undef,
              $wordid);
  (ref($tcr) eq 'ARRAY') or die "Word lacks Han renderings, stopped";

  my @tcs;
  for my $r (@$tcr) {
    push @tcs, (decode('UTF-8', $r->[0],
                Encode::FB_CROAK | Encode::LEAVE_SRC));
  }

  # Get all Pinyin renderings
  my $pcr = $dbh->selectall_arrayref(
              'SELECT pnytext FROM pny WHERE wordid=? '
              . 'ORDER BY pnyord ASC',
              undef,
              $wordid);
  (ref($pcr) eq 'ARRAY') or die "Word lacks Pinyin, stopped";
  
  my @pys;
  for my $r (@$pcr) {
    push @pys, (decode('UTF-8', $r->[0],
                Encode::FB_CROAK | Encode::LEAVE_SRC));
  }

  # Get unique Pinyin renderings with tone removed
  my @pyu;
  for my $p (@pys) {
    my $pu = NFD($p);
    $pu =~ s/[\x{300}\x{301}\x{304}\x{30c}]//g;
    $pu = NFC($pu);
    
    my $already = 0;
    for my $q (@pyu) {
      if ($q eq $pu) {
        $already = 1;
        last;
      }
    }
    unless ($already) {
      push @pyu, ($pu);
    }
  }
  
  # Generate all possible tonal combinations
  my @tonals;
  for(my $x = 0; $x <= $#tcs; $x++) {
    for(my $y = 0; $y <= $#pys; $y++) {
      push @tonals, ($tcs[$x] . ' ' . $pys[$y]);
    }
  }
  
  # Generate all possible toneless combinations
  my @toneless;
  for(my $x = 0; $x <= $#tcs; $x++) {
    for(my $y = 0; $y <= $#pyu; $y++) {
      push @toneless, ($tcs[$x] . ' ' . $pyu[$y]);
    }
  }
  
  # Go through fallback list until we get at least one result or get
  # through the whole list
  my @result;
  for my $fba (@fallback) {
    # Determine correct keylist for this hash
    my $keylistr;
    if ($fba->[0] > 0) {
      $keylistr = \@tonals;
      
    } elsif ($fba->[0] == 0) {
      $keylistr = \@toneless;
      
    } elsif ($fba->[0] < 0) {
      $keylistr = \@tcs;
      
    } else {
      die "Unexpected";
    }
    
    # Look for everything in the keylist and add to results
    for my $checkval (@$keylistr) {
      if (defined($fba->[1]->{$checkval})) {
        push @result, ($fba->[1]->{$checkval});
      }
    }
    
    # If results are non-empty, no need to fallback further
    if ($#result >= 0) {
      last;
    }
  }
  
  # Report if necessary
  if ($#result < 0) {
    print "Missing: $wordid\n";
    
  } elsif ($#result > 1) {
    print "Multiple: $wordid\n";
  }
}

# If we got here, commit the transaction
#
$dbc->finishWork;

=head1 AUTHOR

Noah Johnson, C<noah.johnson@loupmail.com>

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2022 Multimedia Data Technology Inc.

MIT License:

Permission is hereby granted, free of charge, to any person obtaining a
copy of this software and associated documentation files
(the "Software"), to deal in the Software without restriction, including
without limitation the rights to use, copy, modify, merge, publish,
distribute, sublicense, and/or sell copies of the Software, and to
permit persons to whom the Software is furnished to do so, subject to
the following conditions:

The above copyright notice and this permission notice shall be included
in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

=cut
